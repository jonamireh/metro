// Copyright (C) 2024 Zac Sweers
// SPDX-License-Identifier: Apache-2.0
import dev.zacsweers.metro.gradle.DelicateMetroGradleApi
import dev.zacsweers.metro.gradle.ExperimentalMetroGradleApi
import dev.zacsweers.metro.gradle.RequiresIdeSupport
import org.jetbrains.kotlin.gradle.ExperimentalKotlinGradlePluginApi
import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl
import org.jetbrains.kotlin.gradle.dsl.KotlinMultiplatformExtension

plugins {
  alias(libs.plugins.kotlin.multiplatform)
  alias(libs.plugins.android.kmp)
  id("dev.zacsweers.metro")
}

@OptIn(ExperimentalMetroGradleApi::class, DelicateMetroGradleApi::class, RequiresIdeSupport::class)
metro {
  // Until it's possible to disable JS IC
  // https://youtrack.jetbrains.com/issue/KT-82989
  enableTopLevelFunctionInjection.set(false)
  generateContributionHintsInFir.set(false)
  generateContributionHints.set(false)
}

@OptIn(ExperimentalWasmDsl::class, ExperimentalKotlinGradlePluginApi::class)
kotlin {
  android { namespace = "dev.zacsweers.metro.test.integration.android" }

  jvm()

  js { browser() }

  wasmJs { browser() }
  wasmWasi { nodejs() }

  applyDefaultHierarchyTemplate {
    common {
      group("concurrent") {
        withAndroidTarget()
        withJvm()
        withNative()
      }
      group("commonWasm") {
        withWasmJs()
        withWasmWasi()
      }
      group("commonJvm") {
        withAndroidTarget()
        withJvm()
      }
    }
  }

  configureOrCreateNativePlatforms()

  sourceSets {
    commonTest {
      dependencies {
        implementation(libs.kotlin.test)
        implementation(libs.coroutines)
      }
    }
  }

  targets.configureEach {
    compilations.configureEach {
      compileTaskProvider.configure {
        compilerOptions.freeCompilerArgs.addAll(
          // This is irrelevant for these tests and creates a bit of noise
          "-Xwarning-level=SUSPICIOUS_UNUSED_MULTIBINDING:disabled"
        )
      }
    }
  }
}

tasks.withType<Test>().configureEach {
  maxParallelForks = Runtime.getRuntime().availableProcessors() * 2
}

// Sourced from https://kotlinlang.org/docs/native-target-support.html
fun KotlinMultiplatformExtension.configureOrCreateNativePlatforms() {
  // Tier 1
  linuxX64()
  macosArm64()
  iosArm64()
  iosSimulatorArm64()

  // Tier 2
  linuxArm64()
  watchosSimulatorArm64()
  watchosArm64()
  tvosSimulatorArm64()
  tvosArm64()
  iosArm64()

  // Tier 3
  androidNativeArm32()
  androidNativeArm64()
  androidNativeX86()
  androidNativeX64()
  mingwX64()
  watchosDeviceArm64()
}

tasks.withType<Test>().configureEach {
  maxParallelForks = Runtime.getRuntime().availableProcessors() * 2
}
